apply plugin: 'osgi'

description = 'Apache Jena - DBOE'

ext {
    moduleName = 'org.apache.jena.dboe'
}

dependencies {
    compile project(':jena.arq')
    compile project(':jena.base')
    testCompile project(':jena.base')
    testCompile project(path: ':jena.arq', configuration: 'testOutput')
    testCompile project(path: ':jena.base', configuration: 'testOutput')
    testCompile project(path: ':jena.core', configuration: 'testOutput')
}

task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}
artifacts.archives packageTests

configurations {
    testOutput
}

dependencies {
    testOutput sourceSets.test.output
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$moduleName;version=$projectOsgiVersion"
        instruction 'Require-Capability', '''\
            osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",
            osgi.serviceloader;
                filter:="(osgi.serviceloader=org.apache.commons.rdf.api.RDF)";
                resolution:=mandatory; cardinality:=at_least_one
            '''
    }
    appendix 'jpms'
}