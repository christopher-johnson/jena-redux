apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Apache Jena - Core'

ext {
    javaModuleName = 'org.apache.jena.core'
}

javaModule {
    name = javaModuleName
    extraTestModules = ['java.management', 'java.sql']
    hacks {
        patches('org.hamcrest:hamcrest-core', 'org.mockito:mockito-all')
        patches('org.hamcrest:hamcrest-library', 'org.mockito:mockito-all')
    }
}

dependencies {
    patch "org.hamcrest:hamcrest-core:$hamcrestLibraryVersion"
    patch "org.hamcrest:hamcrest-library:$hamcrestLibraryVersion"
    compile project(':jena.base')
    compile project(':jena.iri')
    compile("com.google.guava:guava:$guavaVersion")
    compile("org.apache.servicemix.bundles:org.apache.servicemix.bundles.xerces:$servicemixXercesVersion"){
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    compile("commons-cli:commons-cli:$commonsCliVersion")
    compile("commons-codec:commons-codec:$commonsCodecVersion")
    compile("org.slf4j:slf4j-api:$slf4jVersion")

    testCompile("org.xenei:junit-contracts:$junitContractsVersion") {
        exclude(module: 'commons-cli')
    }
    testCompile project(path: ':jena.base', configuration: 'testOutput')
    testCompile("org.mockito:mockito-all:$mockitoVersion")
    testCompile("org.awaitility:awaitility:$awaitilityVersion")
}

task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}

artifacts.archives packageTests

configurations {
    testOutput
}

dependencies {
    testOutput sourceSets.test.output
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$javaModuleName;version=$projectOsgiVersion"
    }
    appendix 'jpms'
}