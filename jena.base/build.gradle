apply plugin: 'osgi'
apply plugin: 'com.zyxist.chainsaw'

description = 'Apache Jena - Base Common Environment'

ext {
    javaModuleName = 'org.apache.jena.base'
}

javaModule {
    name = javaModuleName
    hacks {
        patches('org.hamcrest:hamcrest-library', 'org.hamcrest:hamcrest-core')
    }
}

dependencies {
    patch "org.hamcrest:hamcrest-library:$hamcrestLibraryVersion"

    compile("com.google.guava:guava:$guavaVersion")
    compile("com.github.andrewoma.dexx:collection:$dexxCollectionVersion")
    compile("commons-io:commons-io:$commonsIoVersion")
    compile("log4j:log4j:$log4jVersion")
    compile("org.apache.commons:commons-csv:$commonsCsvVersion")
    compile("org.apache.commons:commons-lang3:$commonsLangVersion")
    compile("org.slf4j:slf4j-api:$slf4jVersion")

    testCompile("org.awaitility:awaitility:$awaitilityVersion")
}

task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}

artifacts.archives packageTests

configurations {
    testOutput
}

dependencies {
    testOutput sourceSets.test.output
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', javaModuleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "$javaModuleName;version=$projectOsgiVersion"

    }
    appendix 'jpms'
}